import("//build/config/android/config.gni")
import("//build/config/android/rules.gni")
import("//third_party/icu/config.gni")
import("//tools/v8_context_snapshot/v8_context_snapshot.gni")

demo_shell_webview_manifest = "$target_gen_dir/AndroidManifest.xml"

jinja_template("demo_shell_webview_manifest") {
  input = "shell_webview/AndroidManifest.xml"
  output = demo_shell_webview_manifest
}

generate_jni("demo_shell_jni_headers") {
  sources = [
    "java/src/org/chromium/demo_shell/Shell.java",
    "java/src/org/chromium/demo_shell/ShellManager.java",
  ]
}

android_library("demo_shell_java") {
  testonly = true
  deps = [
    "//base:base_java",
    "//base:jni_java",
    "//components/download/internal/common:internal_java",
    "//components/embedder_support/android:content_view_java",
    "//components/embedder_support/android:view_java",
    "//components/viz/service:service_java",
    "//content/public/android:content_java",
    "//media/base/android:media_java",
    "//media/capture/video/android:capture_java",
    "//mojo/public/java:system_java",
    "//net/android:net_java",
    "//ui/android:ui_java",
  ]
  java_files = [
    "java/src/org/chromium/demo_shell/Shell.java",
    "java/src/org/chromium/demo_shell/ShellManager.java",
    "java/src/org/chromium/demo_shell/ShellViewAndroidDelegate.java",
  ]
  lint_suppressions_file = "suppressions.xml"
  annotation_processor_deps = [ "//base/android/jni_generator:jni_processor" ]
}

shared_library("libdemo_shell") {
  testonly = true
  deps = [
    ":demo_shell_jni_headers",
    "//components/crash/content/browser",
    "//shell_demo:demo_content_shell_lib",
    "//shell_demo:demo_shell_pak",
    "//media",
    "//skia",
  ]

  if (is_component_build) {
    deps += [
      "//device/gamepad",
      "//media/midi",
    ]
  }
  sources = [
    "demo_shell_library_loader.cc",
    "demo_shell_manager.cc",
    "demo_shell_manager.h",
    "shell_android.cc",
  ]

  if (is_android) {
    deps += [
      "//base/test:test_support",
      "//components/embedder_support/android:view",
      "//content/test:android_test_message_pump_support",
      "//mojo/public/java/system:test_support",
      "//ui/android",
    ]
  }
  configs -= [ "//build/config/android:hide_all_but_jni_onload" ]
  configs += [ "//build/config/android:hide_all_but_jni" ]
}

android_library("shell_webview_java") {
  testonly = true
  java_files = [
    "shell_webview/src/org/chromium/demo_shell_webview/ShellWebView.java",
  ]
  deps = [
    ":demo_shell_java",
    ":demo_shell_jni_headers",
  ]
}

_jni_registration_header = "$target_gen_dir/demo_shell_jni_registration.h"

generate_jni_registration("demo_shell_jni_registration") {
  testonly = true
  target = ":shell_webview_java"
  header_output = _jni_registration_header
}

android_library("demo_shell_jni_registration_java") {
  testonly = true
  srcjar_deps = [ ":demo_shell_jni_registration" ]
}

_shared_library_list_file =
    "$target_gen_dir/demo_shell_webview.native_libs"
generated_file("demo_shell_webview_shared_library_list") {
  testonly = true
  deps = [
    ":libdemo_shell"
  ]
  outputs = [
    _shared_library_list_file,
  ]
  data_keys = [ "shared_libraries" ]
  walk_keys = [ "shared_libraries_barrier" ]
  rebase = root_build_dir
}

#NativeLibraries.java
write_native_libraries_java("demo_shell_webview_library_loader") {
  testonly = true
  use_final_fields = true
  native_libraries_list_file = _shared_library_list_file
  deps = [
    ":demo_shell_webview_shared_library_list",
  ]
}

#BuildConfig.java
generate_build_config_srcjar("demo_shell_webview_build_config") {
  use_final_fields = true
  enable_multidex = true
  min_sdk_version = "13"
}

android_library("demo_shell_webview_dynamic_java") {
  testonly = true
  srcjar_deps = [ 
    ":demo_shell_webview_library_loader",
    ":demo_shell_webview_build_config", 
  ]
}

dist_jar("all_package_jar") {
  testonly = true
  requires_android = true
  direct_deps_only = false
  use_unprocessed_jars = true
  no_build_hooks = true
  output = "$root_out_dir/all_package.jar"
  deps = [
      ":shell_webview_java",
  ]
}

android_resources("demo_shell_java_resources") {
  testonly = true
  resource_dirs = [ "java/res" ]
  custom_package = "org.chromium.demo_shell"
}

android_java_prebuilt("all_package_jar_java") {
  testonly = true
  jar_path = "$root_out_dir/all_package.jar"
  deps = [
    ":all_package_jar",
  ]
}

dist_aar("demo_shell_webview") {
    testonly = true
    direct_deps_only = true
    native_libraries = [
      "$root_out_dir/libdemo_shell.so",
    ]

    jar_included_patterns = [
        "*/_content/_public/_android/*",
        "*/_content/_shell/_android/*",
        "*/_ui/_android/*",
        "org.chromium.jni_generator/*",
        "org/chromium/base/*", 
        "org/chromium/component/*",
        "org/chromium/components/*",
        "org/chromium/components/embedder_support/view/*",
        "org/chromium/content_public/*",
        "org/chromium/content/*",
        "org/chromium/demo_shell_webview/*",
        "org/chromium/demo_shell/*",
        "org/chromium/device/*",
        "org/chromium/media/*",
        "org/chromium/mojo/*",
        "org/chromium/net/*",
        "org/chromium/service_manager/*",
        "org/chromium/services/*",
        "org/chromium/ui/*",
        "org/chromium/ui/base/*",
        "J/N.*",
    ]

    resource_included_patterns = [ 
      "*/shell_demo/android/*",
      "*/ui/android/*",
    ]

    jar_excluded_patterns = [
        "org/chromium/demo_shell_webview/R*.class",
    ]

    deps = [
        ":demo_shell_jni_registration",
        ":demo_shell_webview_dynamic_java",
        ":all_package_jar_java",
        "//components/crash/content/app:chrome_crashpad_handler_named_as_so",
        ":demo_shell_java_resources",
        "//ui/android:ui_java_resources",
        ":libdemo_shell",
        ":demo_shell_webview_manifest",
    ]
    android_manifest = demo_shell_webview_manifest
    output = "$root_out_dir/demo_shell_webview.aar"

}